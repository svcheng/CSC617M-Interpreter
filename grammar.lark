// =====================
// LEXER
// =====================

%import common.WS
%ignore WS

%import common.SIGNED_INT       -> INT
%import common.SIGNED_FLOAT     -> FLOAT
%import common.ESCAPED_STRING   -> STRVAL
%import common.CNAME            -> IDENTIFIER

BOOLVAL: "true" | "false"
CHARVAL: "'" /[A-Za-z!@#Vjdd$%^&*()_+=\-{}\[\]:;"'<>,.?]/ "'"
// CHARVAL: "'" /[A-Za-z!@#$%^&*()_+=\-{}\[\]:;"'<>,.?\/|\]/ "'"

// Keywords
LET: "let"
VAR: "var"
IF: "if"
ELSE: "else"
FOR: "for"
WHILE: "while"
REPEAT: "repeat"
UNTIL: "until"
RETURN: "return"
MAIN: "main"
RECORD: "record"
INT_T: "int"
FLOAT_T: "float"
BOOL_T: "bool"
STR_T: "str"
CHAR_T: "char"
ARR: "arr"
VOID: "void"
PRINT: "print"
SCAN: "scan"

// =====================
// Program Structure
// =====================

program: const_decs type_decs func_decs main_block

const_decs: (const_dec)*
type_decs: (type_dec)*
func_decs: (func_dec)*

main_block: MAIN ":" block
?block: "{" stmts "}"
stmts: stmt*

?stmt: const_dec
     | var_dec
     | print_stmt
     | scan_stmt
     | assignment
     | conditional
     | for_loop
     | while_loop
     | repeat_loop
     | invocation ";"

// =====================
// Declarations
// =====================

const_dec: LET IDENTIFIER "=" expr ";"
var_dec: VAR IDENTIFIER ":" datatype ";" -> var_dec_no_assign
       | VAR IDENTIFIER "=" expr ";"     -> var_dec_assign

type_dec: RECORD IDENTIFIER "{" field_list "}"
field_list: field ("," field)*
field: IDENTIFIER ":" datatype

func_dec: (datatype | VOID) IDENTIFIER "(" [field_list] ")" "{" stmts_with_return "}"
stmts_with_return: (stmt | return_stmt)*

// =====================
// Types
// =====================
?datatype: not_array_type
         | array_type

not_array_type: basic_type
              | IDENTIFIER
basic_type: INT_T
          | FLOAT_T
          | BOOL_T
          | STR_T
array_type: not_array_type ARR "[" exprs "]"

// =====================
// Statements
// =====================

assignment: lvalue "=" expr ";"

conditional: IF "(" expr ")" block (ELSE block)?

for_loop: FOR "(" IDENTIFIER ";" expr ";" expr ";" expr ")" block
while_loop: WHILE "(" expr ")" block
repeat_loop: REPEAT block UNTIL "(" expr ")" ";"

return_stmt: RETURN expr ";"
           | RETURN ";"

print_stmt: PRINT "(" expr ")" ";"
scan_stmt: SCAN "(" lvalue ")" ";"

?lvalue: IDENTIFIER
       | arr_access
       | field_access

// =====================
// Expressions
// =====================

// ?expr: arith_expr
//      | bool_expr

?scalar: literal
        | IDENTIFIER
        | arr_access
        | field_access
        | invocation

?literal: INT   -> int_literal
        | FLOAT  -> float_literal
        | BOOLVAL -> bool_literal
        | STRVAL -> str_literal

arr_access: IDENTIFIER "[" exprs "]"
field_access: IDENTIFIER "." IDENTIFIER

invocation: IDENTIFIER "(" [exprs] ")"
exprs: expr ("," expr)*


// Arithmetic
// ?arith_expr: arith_expr "+" term   -> add
//            | arith_expr "-" term   -> sub
//            | term

// ?term: term "*" factor            -> mul
//      | term "/" factor            -> div
//      | factor

// ?factor: scalar
//        | "(" arith_expr ")"

// // Relational
// ?rel_expr: rel_expr "==" rel_term  -> eq
//          | rel_expr "!=" rel_term  -> ne
//          | rel_term

// ?rel_term: rel_term "<" rel_factor  -> lt
//          | rel_term "<=" rel_factor -> le
//          | rel_term ">" rel_factor  -> gt
//          | rel_term ">=" rel_factor -> ge
//          | rel_factor

// ?rel_factor: scalar
//            | "(" rel_expr ")"

// // Boolean
// ?bool_expr: bool_expr "||" bool_term    -> lor
//           | bool_term

// ?bool_term: bool_term "&&" bool_factor  -> land
//           | bool_factor

// ?bool_factor: rel_expr
//             | "(" bool_expr ")"
//             | "!" bool_expr             -> lnot

?expr: expr "||" e1      -> lor
     | e1
?e1: e1 "&&" e2          -> land
   | e2
?e2: e2 "==" e3          -> eq
   | e2 "!=" e3          -> ne
   | e3
?e3: e3 "<" e4           -> lt
   | e3 "<=" e4          -> le
   | e3 ">" e4           -> gt
   | e3 ">=" e4          -> ge
   | e4
?e4: e4 "+" e5           -> add
   | e4 "-" e5           -> sub
   | e5
?e5: e5 "*" factor       -> mul
   | e5 "/" factor       -> div
   | factor
?factor: scalar
       | "(" expr ")"
       | "!" expr        -> lnot